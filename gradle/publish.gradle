ext.s3Credentials = {
    accessKey System.getenv('AWS_ACCESS_KEY_ID')
    secretKey System.getenv('AWS_SECRET_ACCESS_KEY')
}

subprojects {
    // Add a per-project function to include maven artifact publication.
    // To enable publication in a sub-project, include 'publishArtifacts()' in its build.
    ext.publishArtifacts = {
        apply plugin: 'maven-publish'

        publishing {
            publications {
                mavanJava(MavenPublication) {
                    from components.java
                    artifact sourcesJar
                    artifact javadocJar
                    pom.withXml {
                        asNode().children().last() + {
                            resolveStrategy = Closure.DELEGATE_FIRST
                            url 'https://github.com/optimizely/java-sdk'
                            licenses {
                                license {
                                    name 'The Apache Software License, Version 2.0'
                                    url 'http://www.apache.org/license/LICENSE-2.0.txt'
                                    distribution 'repo'
                                }
                            }
                            developers {
                                developer {
                                    id 'optimizely'
                                    name 'Optimizely'
                                    email 'developers@optimizely.com'
                                }
                            }
                        }
                    }
                }
            }
        }
 
        publishing {
            repositories {
                maven {
                    url "s3://$mavenS3Bucket"
                    credentials(AwsCredentials, s3Credentials)
                }
            }
        }

        // publish artifacts as part of the release process
        // afterReleaseBuild.dependsOn publish
    }
}
